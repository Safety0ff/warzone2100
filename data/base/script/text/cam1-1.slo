// SCRIPT file for cam1-1.gam

/* General Values */
public		int		player;
public		int		enemy1;
public		int		enemy2;
private		int		numart;

/* structure limit stuff */
public	STRUCTURESTAT		factory, research, oilDerrick, powerGen;
public	STRUCTURESTAT		command;
public	INT			numPow, numExt, numFac, numRes;

/* Win or lose */
public		SOUND		lostSnd;
public		LEVEL		NextLev;
private		GROUP		allPlayer, nearLZ;
private		int		totDroids;

/* Base Under Attack Stuff */
private		STRUCTURE	hitStruc;
private		BASEOBJ		attackerObj;
private		int		t;
public		SOUND		attackSnd1;
public		SOUND		LZSnd;

/* Transport Entry/Exit coords */
public		int		entryX, entryY, exitX, exitY;

/* LZ Message */
public	INTMESSAGE		LZ_MSG;
public	INTMESSAGE		obj1;

/* Briefing stuff */
public	INTMESSAGE		MissionBrief;
public	INTMESSAGE		InFlight;
public	INTMESSAGE		endMsg;	//, winMsg;

/* Proximity: Artifacts */
public		FEATURESTAT	crate;

public		int		art1X, art1Y;
public		SOUND		art1Snd1, art1Snd2;
public		RESEARCHSTAT	art1Comp;	//Heavy Machine Gun
private		FEATURE		art1ID;

/* player Bonus Research topics given at end of mission */
public	int			numResP0;
public	RESEARCHSTAT		resP0[20];	//need to be declared with value >= numResP0, numResP1, etc.
private	INT			count;

/* Enemy Tactics */
public		TEMPLATE	trike;
public		TEMPLATE	buggy;
public		TEMPLATE	bloke;
public		TEMPLATE	jeep;
private		GROUP		enemy1Surprise,enemy1Last,enemy1Rear1,playerForce1;
private		GROUP		enemy1Last1,enemy1Last2;
private		INT		playerX1, playerY1;

/* Triggers: Win or Lose */
trigger winLoseTrig(every, 5);
trigger gotArtTrig((numart == 1), 6);
trigger gameLostTrig(every, 20);

trigger	RTLZStart(wait,0);
trigger	RTLZFreq(every,300);

/* Triggers: Briefing */
trigger vidEndTrig(CALL_VIDEO_QUIT);

/* Triggers: Artifacts */
trigger art1TakeTrig(droidInRange(player, art1X, art1Y, 171), 5);

trigger		every10(every, 20);

//event wonYetEvnt;
event wonYetLZEvnt;
event timeUp;
event art1TakeEvnt;
event	periodicRTLZ;

/* Events: Start */

event startEvnt(CALL_VIDEO_QUIT)	//(CALL_GAMEINIT)
{
	removeMessage(InFlight, MISS_MSG, 0);
	centreViewPos(1152, 10752);
	setRadarZoom(0);
	setLandingZone(8,83,10,85);
	flyTransporterIn(player, entryX, entryY, false);
	setTransporterExit(player, exitX, exitY);

	// give player briefing again (stored)
	addMessage(MissionBrief, MISS_MSG, 0, false);
	flashOff(INTELMAP);
	// Blip Given at start
	addMessage(obj1, PROX_MSG, player, false);
	// allow to build stuff
	setStructureLimits(powerGen, numPow,0);
	setStructureLimits(oilDerrick, numExt,0);
	setStructureLimits(research, numRes,0);
	setStructureLimits(factory, numFac,0);
	setStructureLimits(command, 0, player);
	enableStructure(command, player);
	enableStructure(powerGen,0);
	enableStructure(oilDerrick,0);
	enableStructure(research,0);
	enableStructure(factory,0);
	// no reinforcements yet!!
	setReinforcementTime(-1);
	// add machine gun crate
	art1ID=addFeature(crate, art1X, art1Y);
	//setEventTrigger(art1SeenEvnt, art1SeenTrig);
	setEventTrigger(art1TakeEvnt, art1TakeTrig);
	// set up some AI for artifact pickup trigger
	groupAddArea(enemy1Rear1, enemy1, 4928, 11072, 5184, 11712);	//rear 1 (jeeps)
	orderGroupLoc(enemy1Rear1, DORDER_SCOUT, 4800, 10432);		//move away at start so not seen!
	setEventTrigger(startEvnt, inactive);
}

event flightStart(CALL_GAMEINIT)
{
	addMessage(InFlight, MISS_MSG, 0, true);
	setEventTrigger(flightStart, inactive);
}

/*Events:*/

/* Events: Artifacts */
event art1TakeEvnt(inactive)
{
	numart=numart+1;
	//playSound(art1Snd2, player);
	playSoundPos(art1Snd2, player, art1ID.x, art1ID.y, art1ID.z);
	destroyFeature(art1ID);
	removeMessage(obj1, PROX_MSG, player);
	enableResearch(art1Comp, player);
	// grab all forces and order to follow player
	groupAddArea(enemy1Last, enemy1, 0, 0, 44*128, 93*128);
	// order to fire at will
	setGroupSecondary(enemy1Last, DSO_ATTACK_LEVEL, DSS_ALEV_ALWAYS);	//fire at will
	groupAddArea(enemy1Last, enemy1, 0, 0, 44*128, 32*128);
	orderGroupLoc(enemy1Last, DORDER_SCOUT, 2752, 2752); //attack top
	groupAddArea(enemy1Last1, enemy1, 0, 32*128, 44*128, 64*128);
	groupAddArea(enemy1Last1, enemy1, 576, 1216, 1472, 5056);	//add stuff on hill west
	orderGroupLoc(enemy1Last1, DORDER_SCOUT, 2496,7460); //attack mid
	groupAddArea(enemy1Last2, enemy1, 0, 64*128, 44*128, 93*128);
	groupAddArea(enemy1Last2, enemy1, 4032, 576, 5312, 11584);	//add east side so attack bottom
	orderGroupLoc(enemy1Last2, DORDER_SCOUT, 1856, 10432); //attack bot
	// set retreat point
	setRetreatPoint(enemy1, 1856, 10432);		//retreat to LZ
	setRetreatForce(enemy1, 90);			//set morale to 10%
	setRetreatLeadership(enemy1, 50);		//set leadership chance (small=more likely?)
	// make a force and block LZ!!!!
	groupAddArea(enemy1Surprise, enemy1, 4000, 9000, 6000, 11000);
	orderGroupLoc(enemy1Surprise, DORDER_SCOUT, 1856, 10432);
	// add LZ blip and WAV message
	// Add the proximity message only once
	addMessage(LZ_MSG, PROX_MSG, player, false);
	// New function to periodically add the LZ message	
	setEventTrigger(periodicRTLZ,RTLZStart);
	setEventTrigger(art1TakeEvnt, inactive);
}

event	periodicRTLZ(inactive)
{
	showConsoleText(_("Return to LZ"), player);
	playSoundPos(LZSnd, player, 1216, 10816, 0);	//as in prox1-1.txt
	setEventTrigger(periodicRTLZ,RTLZFreq);
}

/* Events: Win or Lose */

event nextLevEvnt(inactive)		//assumes victory already checked
{
	pause(20);		//increased to allow all audio before won
	// give bonus research (if req'd)
	count = 0;
	while (count < numResP0)
	{
		enableResearch(resP0[count], 0);
		count = count +1;
	}

	gameLevel=gameLevel+1;		//flag next part of map
	enableResearch(art1Comp, player);
	startMission(BETWEEN, NextLev);
	setEventTrigger(nextLevEvnt, inactive);
}

event gameLost(inactive)
{
	gameOverMessage(endMsg, MISS_MSG, 0, false);
	setEventTrigger(gameLost, inactive);
}

event lostYetEvnt(winLoseTrig)
{
	if (not anyDroidsLeft(player))
	{
		if (not anyStructButWallsLeft(player))
		{
			//setEventTrigger(wonYetEvnt,inactive);
			setEventTrigger(wonYetLZEvnt, inactive);
			setEventTrigger(timeUp, inactive);
			setEventTrigger(gameLost, gameLostTrig);	//waits 2 seconds before ending
			setEventTrigger(lostYetEvnt, inactive);
		}
	}
}

//out of time?
event timeUp(CALL_MISSION_TIME)
{
	//setEventTrigger(wonYetEvnt,inactive);
	setEventTrigger(wonYetLZEvnt, inactive);
	setEventTrigger(lostYetEvnt,inactive);
	setEventTrigger(gameLost, gameLostTrig);	//waits 2 seconds before ending
	setEventTrigger(timeUp, inactive);
}

event wonYetLZEvnt(gotArtTrig)	//got artifact yet?
{
	//check back at LZ and no scavs compromising it!
	totDroids = numDroidsInArea(player, 0, 0, 48*128, 96*128);
	if (totDroids == numDroidsInArea(player, 704, 10048, 1984, 11328))
	{
		if (totDroids!=0)
		{
			//if (numart==1)
			if (numDroidsInArea(enemy1, 704, 10048, 1984, 11328) == 0)

			{
				setEventTrigger(lostYetEvnt, inactive);
				setEventTrigger(nextLevEvnt, winLoseTrig);
				//setEventTrigger(wonYetEvnt, inactive);
				setEventTrigger(wonYetLZEvnt, inactive);
			}
		}
	}
}

event cheatEvnt(CALL_MISSION_START)	//cheat button ctrl M
{
	setEventTrigger(nextLevEvnt, winLoseTrig);
	setEventTrigger(cheatEvnt, inactive);
}

/* Base Under Attack */
event baseHit(CALL_STRUCT_ATTACKED, selectedPlayer, ref hitStruc, ref attackerObj)
{
	if (t >= 20)
	{
		t=0;
		if (hitStruc != NULLOBJECT)
		{
			playSoundPos(attackSnd1, selectedPlayer, hitStruc.x, hitStruc.y, hitStruc.z);	//show position if still alive
		}
		else
		{
			playSound(attackSnd1, selectedPlayer);
		}
	}
}

event everySec(every, 10)
{
	t=t+1;
}

event seeBaseHit(CALL_MISSION_END)
{
	// go to where the structure being attacked is on CTRL B
	if (hitStruc!=NULLOBJECT)
	{
		centreView(hitStruc);
		t=0;			//flag known about!
	}
}
