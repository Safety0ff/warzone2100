/* Script file for Cam1-1S (to start off world map) */
/* Simply place transporter and show briefing */

/* General Values */
public		int		player;

/* structure limit stuff */
public	STRUCTURESTAT		factory, research, oilDerrick, powerGen;
public	STRUCTURESTAT		command;
public	INT			numPow, numExt, numFac, numRes;

/* Briefing stuff */
public	INTMESSAGE		MissionBrief, MissionBrief2;
public	INTMESSAGE		InFlight;
public	INTMESSAGE		endMsg;
public		LEVEL		NextLev;
public		TEMPLATE	transporter;
public		SOUND		lostSnd;
public		INT		timeLimit;

/* Landing Zone coords for Map to follow */
public		INT		LZ_X1, LZ_Y1, LZ_X2, LZ_Y2;
private		INT		temp;
private		BOOL		powFlag;

public		int		exitX, exitY;

/* Triggers: Briefing */
trigger vidEndTrig(CALL_VIDEO_QUIT);
trigger skipTrig(wait, 10);
trigger powModTrig(testStructureModule(player, NULLOBJECT, temp), 30);
trigger gameLostTrig(every, 20);

event nextLevEvnt;
event waitForPow;

event startEvnt(CALL_GAMEINIT)
{
	centreViewPos(1728, 6720);			//centre near transport
	setRadarZoom(0);
	// stop player building on LZ
	setLandingZone(10, 51, 12, 53);
	// allow to build stuff
	setStructureLimits (powerGen,numPow,0);
	setStructureLimits (oilDerrick,numExt,0);
	setStructureLimits (research,numRes,0);
	setStructureLimits (factory,numFac,0);
	setStructureLimits(command, 1, player);
	enableStructure(command, player);
	enableStructure(powerGen,0);
	enableStructure(oilDerrick,0);
	enableStructure(research,0);
	enableStructure(factory,0);
	setMissionTime(-1);			//reset Mission time limit

	//has the player already built a power module? (skip 1st briefing, go straight to main briefing)
	if (testStructureModule(player, NULLOBJECT, temp))
	{
		setEventTrigger(waitForPow, skipTrig);
		//setEventTrigger(powModCheck, inactive);
	}
	else
	{
		addMessage(MissionBrief, MISS_MSG, 0, true);
		powFlag = TRUE;
	}
	setEventTrigger(startEvnt, inactive);
}

//needs to trigger when power gen updated or timed out (5 mins?)
event waitForPow(powModTrig)	//(wait, 3000)
{
	if (powFlag)
	{
		removeMessage(MissionBrief, MISS_MSG, 0);
	}
	setEventTrigger(waitForPow, inactive);
	addMessage(MissionBrief2, MISS_MSG, 0, true);
	addDroid(transporter,1472,6720,player);		//place transporter!!!

	setTransporterExit(player, exitX, exitY);

	setMissionTime(timeLimit);			//set Mission time limit
	centreViewPos(1728, 6720);			//centre near transport
}

//allow skip past delay
event cheatEvnt(CALL_MISSION_START)	//cheat button ctrl M
{
	setEventTrigger(waitForPow, skipTrig);
	setEventTrigger(cheatEvnt, inactive);
}

event nextLevEvnt(CALL_START_NEXT_LEVEL)		//assumes victory already checked
{
	startMission(OFF_KEEP, NextLev);
	setEventTrigger(nextLevEvnt, inactive);
}

event gameLost(inactive)
{
	gameOverMessage(endMsg, MISS_MSG, 0, false);
	setEventTrigger(gameLost, inactive);
}

//out of time?
event lostYetEvnt(CALL_MISSION_TIME)
{
	setEventTrigger(nextLevEvnt,inactive);
	setEventTrigger(lostYetEvnt, inactive);
	setEventTrigger(gameLost, gameLostTrig);	//waits 2 seconds before ending
}
